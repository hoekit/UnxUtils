dnl Process this file with autoconf to produce a configure script.
AC_INIT(which.c)
AM_INIT_AUTOMAKE(which, 2.4)
AM_CONFIG_HEADER(config.h)

which_cv_readline=yes
which_cv_iberty=yes
AC_ARG_ENABLE(readline,
  [  --disable-readline      do not try to find and use the readline library],
  which_cv_readline=$enableval)
AC_ARG_ENABLE(iberty,
  [  --disable-iberty        do not try to find and use the iberty library],
  which_cv_iberty=$enableval)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_LANG_C

dnl Checks for libraries.
if test "$which_cv_readline" = yes ; then
  AC_CHECK_STATICLIB(libreadline.a, tilde_expand)
fi
if test "$which_cv_iberty" = yes ; then
  AC_CHECK_STATICLIB(libiberty.a, xmalloc)
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_FUNCS(strchr memcpy getopt_long)
AC_CHECK_HEADERS(unistd.h string.h libiberty.h readline/tilde.h)
AC_HEADER_STAT

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_TYPE_GETGROUPS
AC_CHECK_FUNCS(getcwd)

if test "$ac_cv_lib_static_libiberty_a_xmalloc" = no \
     -o "$which_cv_iberty" = no ; then
  AC_DEFINE(NEED_XMALLOC)
fi
if test "$ac_cv_lib_static_libreadline_a_tilde_expand" = no \
     -o "$which_cv_readline" = no ; then
  AC_DEFINE(NEED_TILDE_EXPAND)
fi

AC_OUTPUT(Makefile)
